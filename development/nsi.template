;Template file for generating NSIS .nsi file
;NOTE: Add Python, PyYAML and OpenAL installers under "depends" in root directory to create offline installer.
;Defines
!define PRODUCT_NAME "Unknown Horizons"
!define PRODUCT_VERSION "%s"
!define UH_ICON "$INSTDIR\content\gfx\uh.ico"
!define PRODUCT_ROOT_KEY "HKLM"
!define PRODUCT_SUBKEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"

!include "FileFunc.nsh"
!include "WordFunc.nsh"
!include "LogicLib.nsh"

;MUI2
!include "MUI2.nsh"

!define MUI_ICON "content\gfx\uh.ico"
!define MUI_ABORTWARNING
!define MUI_ABORTWARNING_CANCEL_DEFAULT
!define MUI_HEADERIMAGE
!define MUI_HEADERIMAGE_RIGHT
!define MUI_HEADERIMAGE_BITMAP "content\gfx\header.bmp"
!define MUI_WELCOMEFINISHPAGE_BITMAP "content\gfx\banner.bmp"

;Install pages
!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_LICENSE "doc\LICENSE"
!insertmacro MUI_PAGE_COMPONENTS
!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_INSTFILES

;Uninstall pages
!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES

;Language
!insertmacro MUI_LANGUAGE "English"

;Installer Info
Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "Setup.exe"
InstallDir "$PROGRAMFILES\Unknown Horizons"
BrandingText "${PRODUCT_NAME} ${PRODUCT_VERSION} Installer"
ShowInstDetails hide
ShowUninstDetails hide

Section "Unknown Horizons" uh
	SetOutPath "$INSTDIR"
	SetOverwrite try
%s
SectionEnd

Section "-Misc"
	WriteUninstaller "$INSTDIR\uninst.exe"
	WriteRegStr "${PRODUCT_ROOT_KEY}" "${PRODUCT_SUBKEY}" "DisplayName" "${PRODUCT_NAME}"
	WriteRegStr "${PRODUCT_ROOT_KEY}" "${PRODUCT_SUBKEY}" "UninstallString" "$INSTDIR\uninst.exe"
	WriteRegStr "${PRODUCT_ROOT_KEY}" "${PRODUCT_SUBKEY}" "DisplayIcon" ${UH_ICON}
	WriteRegStr "${PRODUCT_ROOT_KEY}" "${PRODUCT_SUBKEY}" "Publisher" "The Unknown Horizons Team"
	WriteRegStr "${PRODUCT_ROOT_KEY}" "${PRODUCT_SUBKEY}" "DisplayVersion" "${PRODUCT_VERSION}"
	WriteRegStr "${PRODUCT_ROOT_KEY}" "${PRODUCT_SUBKEY}" "URLInfoAbout" "http://www.unknown-horizons.org"
	${GetSize} "$INSTDIR" "/S=0K" $0 $1 $2
	IntFmt $0 "%s" $0
	WriteRegDWORD "${PRODUCT_ROOT_KEY}" "${PRODUCT_SUBKEY}" "EstimatedSize" "$0"

	SetOutPath "$INSTDIR"
	CreateDirectory "$SMPROGRAMS\Unknown Horizons"
	CreateShortCut "$SMPROGRAMS\Unknown Horizons\Unknown Horizons.lnk" "$INSTDIR\run_uh.bat" "" "${UH_ICON}"
	CreateShortCut "$SMPROGRAMS\Unknown Horizons\Uninstall.lnk" "$INSTDIR\uninst.exe"
	CreateShortCut "$DESKTOP\Unknown Horizons.lnk" "$INSTDIR\run_uh.bat" "" "${UH_ICON}"
SectionEnd

Section "Uninstall"
	Delete "$INSTDIR\uninst.exe"
	Delete "$INSTDIR\fife.log"
%s
	Delete "$SMPROGRAMS\Unknown Horizons\Unknown Horizons.lnk"
	Delete "$SMPROGRAMS\Unknown Horizons\Uninstall.lnk"
	Delete "$DESKTOP\Unknown Horizons.lnk"
	RMDir "$SMPROGRAMS\Unknown Horizons"
%s
	RMDir "$INSTDIR"
	DeleteRegKey "${PRODUCT_ROOT_KEY}" "${PRODUCT_SUBKEY}"
SectionEnd

Section "OpenAL" OpenAL_Sec
	SetOutPath "$TEMP"
	File /nonfatal "depends\oalinst.exe"
	Goto downloadOpenAL

downloadOpenAL:
	IfFileExists "$TEMP\oalinst.exe" installOpenAL

	NSISdl::download http://connect.creativelabs.com/openal/Downloads/oalinst.zip "$TEMP\oalinst.zip"
	Pop $R0
	StrCmp $R0 "success" +3
		MessageBox MB_OK "Download failed: $R0"
		Quit

	;Need nsisunz plugin to unzip the file.
	nsisunz::Unzip "$TEMP\oalinst.zip" "$TEMP"
	Pop $R0
	StrCmp $R0 "success" +2
		DetailPrint "$R0"
	Goto installOpenAL

installOpenAL:
	ExecWait '$TEMP\oalinst.exe'
	Delete "$TEMP\oalinst.exe"

SectionEnd

Section "Python" Python_Sec
	SetOutPath "$TEMP"
	File /nonfatal "depends\python-2.6.6.msi"
	Goto downloadPython

downloadPython:
	IfFileExists "$TEMP\python-2.6.6.msi" installPython

	NSISdl::download http://www.python.org/ftp/python/2.6.6/python-2.6.6.msi "$TEMP\python-2.6.6.msi"
	Pop $R0
	StrCmp $R0 "success" +3
		MessageBox MB_OK "Download failed: $R0"
		Quit
	Goto installPython

installPython:
	ExecWait '"msiexec" /i "$TEMP\python-2.6.6.msi" /passive'
	Delete "$TEMP\python-2.6.6.msi"

SectionEnd

Section "PyYAML" PyYAML_Sec
	SetOutPath "$TEMP"
	File /nonfatal "depends\PyYAML-3.09.win32-py2.6.exe"
	Goto downloadPyYAML

downloadPyYAML:
	IfFileExists "$TEMP\PyYAML-3.09.win32-py2.6.exe" installPyYAML

	NSISdl::download http://pyyaml.org/download/pyyaml/PyYAML-3.09.win32-py2.6.exe "$TEMP\PyYAML-3.09.win32-py2.6.exe"
	Pop $R0
	StrCmp $R0 "success" +3
		MessageBox MB_OK "Download failed: $R0"
		Quit
	Goto installPyYAML

installPyYAML:
	ExecWait '$TEMP\PyYAML-3.09.win32-py2.6.exe'
	Delete "$TEMP\PyYAML-3.09.win32-py2.6.exe"

SectionEnd

LangString DESC_uh ${LANG_ENGLISH} "Unknown Horizons - The game itself including FIFE. (Required)"
LangString DESC_OpenAL ${LANG_ENGLISH} "OpenAL: Necessary for music and sound! Choose this one if it isn't installed already."
LangString DESC_Python ${LANG_ENGLISH} "Python: Necessary to play UH! Choose this one if it isn't installed already."
LangString DESC_PyYAML ${LANG_ENGLISH} "PyYAML: Necessary to play UH! Choose this one if it isn't installed already. Needs Python."

!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
	!insertmacro MUI_DESCRIPTION_TEXT ${uh} $(DESC_uh)
	!insertmacro MUI_DESCRIPTION_TEXT ${OpenAL_Sec} $(DESC_OpenAL)
	!insertmacro MUI_DESCRIPTION_TEXT ${Python_Sec} $(DESC_Python)
	!insertmacro MUI_DESCRIPTION_TEXT ${PyYAML_Sec} $(DESC_PyYAML)
!insertmacro MUI_FUNCTION_DESCRIPTION_END

Function .onInit
	ClearErrors
	ReadRegStr $3 "${PRODUCT_ROOT_KEY}" "${PRODUCT_SUBKEY}" "DisplayVersion"
	IfErrors done
	${VersionCompare} $3 "${PRODUCT_VERSION}" $4
	IntCmp $4 1 newerExists newerExists olderExists

olderExists:
	MessageBox MB_OKCANCEL|MB_ICONEXCLAMATION "An older version, $3 of Unknown Horizons is installed on your system. $\n The older version will be removed. Click Cancel to exit setup." IDCANCEL Quit
	ClearErrors
	ReadRegStr $5 "${PRODUCT_ROOT_KEY}" "${PRODUCT_SUBKEY}" "UninstallString"
	${GetParent} "$5" $6
	CopyFiles /SILENT "$5" "$TEMP\uninst_uh.exe"
	ExecWait '$TEMP\uninst_uh.exe _?=$6'
	Delete "$TEMP\uninst_uh.exe"
	Goto done

newerExists:
	MessageBox MB_OK|MB_ICONEXCLAMATION "Unknown Horizons ${PRODUCT_VERSION} or newer is already installed on the system. Please uninstall that version and try again." IDOK Quit

Quit:
	Quit

done:
	SectionSetFlags ${uh} 17
	ReadRegStr $3 "HKLM" "Software\Microsoft\Windows\CurrentVersion\Uninstall\OpenAL" "DisplayName"
	${If} $3 != ""
		!insertmacro UnselectSection ${OpenAL_Sec}
	${EndIf}
	!insertmacro UnselectSection ${Python_Sec}
	!insertmacro UnselectSection ${PyYAML_Sec}
	SetOutPath $TEMP
	File "content\gfx\inst_splash.bmp"
	advsplash::show 1300 500 400 0xff00ff "$TEMP\inst_splash"
	Delete "$TEMP\inst_splash.bmp"
FunctionEnd

Function un.onUninstSuccess
	MessageBox MB_ICONINFORMATION|MB_OK "Please note: Python, PyYAML and OpenAL have to be removed manually if you don't need them anymore."
	MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) has been successfully uninstalled."
FunctionEnd
